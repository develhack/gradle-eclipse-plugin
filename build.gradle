plugins {
	id 'groovy'
	id 'maven-publish'
	id 'eclipse'
	id "com.jfrog.bintray" version "0.6"
}

group = 'com.develhack'
archivesBaseName = 'gradle-eclupse-plugin'
version = '0.1.4'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories { jcenter() }

dependencies {
	compile     gradleApi()
	compile     localGroovy()
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
	classifier = 'javadoc'
	from "${buildDir}/javadoc"
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			from components.java

			artifact sourcesJar { classifier "sources" }

			artifact javadocJar { classifier "javadoc" }
		}
	}
}

if(hasProperty('BINTRAY_USER') && hasProperty('BINTRAY_API_KEY')) {

	bintray {
		user = "$BINTRAY_USER"
		key = "$BINTRAY_API_KEY"
		publications = ['mavenJava']
		pkg {
			userOrg = 'develhack'
			repo = 'maven'
			name = 'gradle-eclipse-plugin'
			version {
				attributes = ['gradle-plugin': 'com.develhack.eclipse:com.develhack:gradle-eclipse-plugin' ]
			}
		}
	}
}

defaultTasks 'clean', 'build'